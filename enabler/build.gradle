buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:8.1.0'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'nebula.maven-base-publish'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    compile 'com.google.guava:guava:25.1-android'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'org.json:json:20090211' // provided by Android
    compile 'net.sf.kxml:kxml2:2.3.0' // provided by Android
    testCompile 'junit:junit:4.12'
    testRuntime 'org.slf4j:slf4j-jdk14:1.7.25'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['src']
    }
    test {
        java.srcDirs = ['test']
        resources.srcDirs = ['test']
    }
}

compileJava {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
    options.encoding = 'UTF-8'
}

test {
    exclude 'de/schildbach/pte/live/**'
}

def pomConfig = {
    licenses {
        license {
            name "GNU General Public License, Version 3.0"
            url "https://www.gnu.org/licenses/gpl-3.0.html"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "opentransitmap"
            name "OpenTransitMap"
            email "nicoalt@posteo.org"
        }
    }

    scm {
       url "https://gitlab.com/opentransitmap/public-transport-enabler"
    }
}

publishing {
  publications {
    nebula(MavenPublication) {
    }
  }
}

def getCheckedOutGitCommitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

bintray {
	user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
	publications = ['nebula']
	pkg {
		repo = 'public-transport-enabler'
		name = 'public-transport-enabler'
		userOrg = 'opentransitmap'
		licenses = ['GPL-3.0']
		vcsUrl = 'https://gitlab.com/opentransitmap/public-transport-enabler.git'
		version {
			name = getCheckedOutGitCommitHash()
			desc = 'OpenTransitMap\'s Public Transport Enabler'
			attributes = ['gradle-plugin': 'org.opentransitmap:public-transport-enabler']
		}
	}
}
